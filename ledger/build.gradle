buildscript {
    dependencies {
        // For publishing on Bintray
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.1'
    }
}
apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

android {
    // Android API levels
    compileSdkVersion    28     // what we compile against
    defaultConfig {
        minSdkVersion    21     // minimum supported version
        targetSdkVersion 28     // target feature version
        versionCode versionCode // from gradle.properties
        versionName versionName // from gradle.properties
    }

    // Android NDK version
    // Must match the one in the Docker image
    ndkVersion "20.0.5594570"

    // CMake build of the native executable
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
            // Must match the one the Docker image
            version "3.17.3"
        }
    }
}

dependencies {
    // @NonNull and other annotations
    implementation 'com.android.support:support-annotations:26.1.0'
}

// Generating additional JARs for JCenter

task generateSourceJar(type: Jar) {
    archiveClassifier = 'source'
    from android.sourceSets.main.java.srcDirs
}

task generateJavadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
}

task generateJavadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from generateJavadoc.destinationDir
    dependsOn 'generateJavadoc'
}

// Publishing on Bintray

publishing {
    publications {
        LedgerCLI(MavenPublication) {
            artifact("build/outputs/aar/ledger-release.aar")
            artifact generateSourceJar
            artifact generateJavadocJar
            groupId 'net.ilammy.ledger'
            artifactId 'ledger-cli'
            version versionName // from gradle.properties
        }
    }
}

bintray {
    // Bintray publishing credentials must be supplied via environment variables
    user = System.getenv('BINTRAY_USER')    // Bintray username
    key  = System.getenv('BINTRAY_API_KEY') // Bintray API Key: https://bintray.com/profile/edit > API key
    publications = ['LedgerCLI']
    pkg {
        repo = 'ledger'
        name = 'ledger-cli'
        desc = 'Ledger CLI binary for Android'
        licenses = ['GPL-3.0-or-later']
        vcsUrl = 'https://git.sr.ht/~ilammy/android-ledger-cli'
        issueTrackerUrl = 'https://todo.sr.ht/~ilammy/android-ledger'
        websiteUrl = 'https://www.ledger-cli.org'
        githubRepo = 'ilammy/android-ledger-cli' // mirror
        githubReleaseNotesFile = 'CHANGELOG.md'
        labels = ['Android', 'AAR', 'Ledger']
        publish = false // publish uploads from Bintray site manually
        version {
            name = versionName // from gradle.properties
            desc = versionDesc // from gradle.properties
            vcsTag = 'v' + versionName
            released = new Date()
            gpg {
                sign = true
            }
        }
    }
}
